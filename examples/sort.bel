extract min from (list) to [min] and leaving [rest] {
	head of (list) is [head],
	tail of (list) is [tail],
	{
		is (tail) pair,
		extract min from (tail) to [submin] and leaving [subrest],
		{
			(head) < (submin),
			[min] = (head),
			pair of (submin) and (subrest) is [newrest],
			[rest] = (newrest)
		}.
		{
			(head) >= (submin),
			[min] = (submin),
			pair of (head) and (subrest) is [newrest],
			[rest] = (newrest)
		}
	}.
	{
		is not (tail) pair,
		{
			(head) < (tail),
			[min] = (head),
			[rest] = (tail)
		}.
		{
			(head) >= (tail),
			[min] = (tail),
			[rest] = (head)
		}
	}
}

sort (list) to [sorted] {
	{
		is not (list) pair,
		[sorted] = (list)
	}.
	{
		is (list) pair,
		extract min from (list) to [min] and leaving [rest],
		sort (rest) to [subsorted],
		pair of (min) and (subsorted) is [newsorted],
		[sorted] = (newsorted)
	}
}

{
	pair of (5) and (3) is [list1],
	pair of (8) and (list1) is [list2],
	pair of (1) and (list2) is [list3],
	pair of (4) and (list3) is [list4],
	sort (list4) to [sorted],
	log (sorted)
}
